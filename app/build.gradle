apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'realm-android'

def APP_ID = 'silin.theMovieDB_2_0'

// Versioning
def versionMajor = 1
def versionMinor = 0
def versionPatch = 0

// SDK versioning
def LATEST_SDK_VER = 24
def MIN_SDK_DEV_VER = 21
def MIN_SDK_PROD_VER = 18

// Build tool versioning
def BUILD_TOOL_VER = "24.0.0"

// Git commit hash and build time
def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm:ss'Z'", TimeZone.getTimeZone("UTC"))

// HTTP logging level by build
def ENVIRONMENT_CONSTANT_TYPE = APP_ID + ".IEnvironment"
def ENVIRONMENT_CONSTANT_NAME = "ENVIRONMENT"
def ENVIRONMENT_CONSTANT_VALUE_DEV = APP_ID + ".Environment.DEV"
def ENVIRONMENT_CONSTANT_VALUE_PROD = APP_ID + ".Environment.PROD"

android {
    compileSdkVersion LATEST_SDK_VER
    buildToolsVersion BUILD_TOOL_VER
    dataBinding {
        enabled = true
    }
    defaultConfig {
        applicationId APP_ID
        targetSdkVersion LATEST_SDK_VER
        vectorDrawables.useSupportLibrary = true
        versionCode versionMajor * 1000 + versionMinor * 100 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
    }
    productFlavors {
        dev {
            minSdkVersion MIN_SDK_DEV_VER
        }
        prod {
            minSdkVersion MIN_SDK_PROD_VER
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            resValue "string", "app_name", "theMovieDB_2_0 (Dev)"

            testCoverageEnabled = true

            buildConfigField ENVIRONMENT_CONSTANT_TYPE, ENVIRONMENT_CONSTANT_NAME, ENVIRONMENT_CONSTANT_VALUE_DEV

            // REST API setting from gradle.properties
            buildConfigField API_BASE_URL_CONSTANT_TYPE, API_BASE_URL_CONSTANT_NAME, API_BASE_URL_CONSTANT_VALUE_DEV
            // buildConfigField API_BASE_HEADER_CONSTANT_TYPE, API_BASE_HEADER_CONSTANT_NAME, API_BASE_HEADER_CONSTANT_VALUE_DEV
            buildConfigField API_BASE_KEY_CONSTANT_TYPE, API_BASE_KEY_CONSTANT_NAME, API_BASE_KEY_CONSTANT_VALUE_DEV
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            resValue "string", "app_name", "theMovieDB_2_0"

            buildConfigField ENVIRONMENT_CONSTANT_TYPE, ENVIRONMENT_CONSTANT_NAME, ENVIRONMENT_CONSTANT_VALUE_PROD

            // REST API setting
            buildConfigField API_BASE_URL_CONSTANT_TYPE, API_BASE_URL_CONSTANT_NAME, API_BASE_URL_CONSTANT_VALUE_PROD
            // buildConfigField API_BASE_HEADER_CONSTANT_TYPE, API_BASE_HEADER_CONSTANT_NAME, API_BASE_HEADER_CONSTANT_VALUE_PROD
            buildConfigField API_BASE_KEY_CONSTANT_TYPE, API_BASE_KEY_CONSTANT_NAME, API_BASE_KEY_CONSTANT_VALUE_PROD
        }
    }

    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
            outputs.upToDateWhen { false }
            showStandardStreams = true
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')

    compile 'com.android.support:design:24.0.0'
    compile 'com.android.support:support-annotations:24.0.0'
    compile 'com.android.support:support-vector-drawable:24.0.0'
    compile 'com.android.support:percent:24.0.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.0-alpha3'

    apt 'com.squareup:javapoet:1.7.0'

    provided 'com.google.auto.value:auto-value:1.2'
    compile 'com.ryanharter.auto.value:auto-value-parcel-adapter:0.2.3-rc2'
    apt 'com.google.auto.value:auto-value:1.2'
    apt 'com.ryanharter.auto.value:auto-value-parcel:0.2.3-rc2'
    apt 'com.ryanharter.auto.value:auto-value-moshi:0.3.3-rc1'

    compile 'com.squareup.okhttp3:okhttp:3.3.1'
    compile group: 'com.squareup.okhttp3', name: 'logging-interceptor', version: '3.3.1'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.squareup.moshi:moshi:1.2.0'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-moshi:2.1.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'

    compile 'com.jakewharton:butterknife:8.1.0'
    apt 'com.jakewharton:butterknife-compiler:8.1.0'

    compile 'com.google.dagger:dagger:2.2'
    compile 'org.glassfish:javax.annotation:10.0-b28'
    provided 'javax.annotation:jsr250-api:1.0'
    apt 'com.google.dagger:dagger-compiler:2.2'

    compile 'com.github.lukaspili.autodagger2:autodagger2:1.1'
    apt 'com.github.lukaspili.autodagger2:autodagger2-compiler:1.1'

    compile 'se.emilsjolander:intentbuilder-api:0.14.0'
    apt 'se.emilsjolander:intentbuilder-compiler:0.14.0'

    compile 'frankiesardo:icepick:3.2.0'
    provided 'frankiesardo:icepick-processor:3.2.0'

    compile 'com.vanniktech:onactivityresult:0.6.0'
    apt 'com.vanniktech:onactivityresult-compiler:0.6.0'

    compile 'io.reactivex:rxjava:1.1.6'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'com.trello:rxlifecycle:0.6.1'
    compile 'com.trello:rxlifecycle-components:0.6.1'

    compile 'com.hannesdorfmann.mosby:mvp:2.0.1'
    compile 'com.hannesdorfmann.mosby:viewstate:2.0.1'

    compile 'com.birbit:android-priority-jobqueue:1.3.5'

    compile 'com.facebook.rebound:rebound:0.3.8'

    compile 'uk.co.chrisjenx:calligraphy:2.2.0'
    compile 'com.novoda:merlin:0.8.0'
    compile 'joda-time:joda-time:2.3'

    compile 'io.nlopez.smartadapters:library:1.3.1'
    compile 'me.relex:circleindicator:1.2.1@aar'

    debugCompile 'com.facebook.stetho:stetho:1.3.1'
    debugCompile 'com.facebook.stetho:stetho-okhttp3:1.3.1'
    debugCompile 'com.jakewharton.scalpel:scalpel:1.1.2'

    compile 'io.palaima.debugdrawer:debugdrawer-commons:0.7.0'
    compile 'io.palaima.debugdrawer:debugdrawer-okhttp:0.7.0'
    compile 'io.palaima.debugdrawer:debugdrawer-okhttp3:0.7.0'
    compile 'io.palaima.debugdrawer:debugdrawer-picasso:0.7.0'
    compile 'io.palaima.debugdrawer:debugdrawer-scalpel:0.7.0'
    compile 'io.palaima.debugdrawer:debugdrawer-location:0.7.0'
    debugCompile 'io.palaima.debugdrawer:debugdrawer:0.7.0'
    debugCompile 'io.palaima.debugdrawer:debugdrawer-view:0.7.0'
    releaseCompile 'io.palaima.debugdrawer:debugdrawer-no-op:0.7.0'
    releaseCompile 'io.palaima.debugdrawer:debugdrawer-view-no-op:0.7.0'

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-beta2'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'

    androidTestCompile 'com.android.support:support-annotations:24.0.0'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
    androidTestCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'junit:junit:4.12'
    testCompile 'com.squareup.okhttp3:mockwebserver:3.3.1'
}
